#version 430

in vec4 Position;
in vec3 Normal;
in vec2 TexCoord;

// The texture containing the result of the 1st pass
layout (binding=0) uniform sampler2D RenderTex;
layout (binding=1) uniform sampler2D NoiseTex;

uniform float EdgeThreshold; // The squared threshold

// Select functionality: pass1 or pass2
subroutine vec4 RenderPassType();
subroutine uniform RenderPassType RenderPass;

struct LightInfo {
    vec4 Position;  // Light position in eye coords
    vec3 Intensity; // Light intensity
};
uniform LightInfo Light;

struct MaterialInfo {
    vec3  Ka;        // Ambient  reflectivity
    vec3  Kd;        // Diffuse  reflectivity
    vec3  Ks;        // Specular reflectivity
    float Shininess; // Specular shininess factor
};
uniform MaterialInfo Material;

uniform float Width;
uniform float Height;
uniform float Radius;


const vec3 lum = vec3(0.2126, 0.7152, 0.0722);

out vec4 FragColor;

float luminance(vec3 color) {
    return dot(lum, color);
}

vec3 phongModel ( vec4 position, vec3 normal ) {
    vec3 s         = normalize(vec3(Light.Position - position));
    vec3 v         = normalize(-position.xyz); // In eyeCoords, the viewer is at the origin -> only take negation of eyeCoords vector
    vec3 h         = normalize (v+s);

    vec3  ambient  = Light.Intensity * Material.Ka;

    float sDotN    = max(dot(s, normal), 0.0);
    vec3  diffuse  = Light.Intensity * Material.Kd * sDotN;
    vec3  spec     = vec3(0.0);
    if (sDotN > 0.0) {
        spec = Light.Intensity * Material.Ks * pow(max(dot(h,normal), 0.0), Material.Shininess);
    }

    return ambient +  diffuse + spec;
}

subroutine (RenderPassType)
vec4 pass1() {
    return vec4(phongModel(Position, Normal), 1.0);
    //return vec4 (1.0, 0.0, 0.0, 1.0);
}

subroutine (RenderPassType)
vec4 pass2() {
    vec4  noise = texture(NoiseTex,  TexCoord);
    vec4  color = texture(RenderTex, TexCoord);
    float green = luminance(color.rgb);

    float dist1 = length(gl_FragCoord.xy - vec2(0.25 * Width, 0.5 * Height));
    float dist2 = length(gl_FragCoord.xy - vec2(3 * 0.25 * Width, 0.5 * Height));
    if ((dist1 > Radius) && (dist2 > Radius)) green = 0.0;

    return vec4(0.0, green * clamp(noise.a + 0.25, 0, 1), 0.0, 1.0);
}


void main()
{    
    FragColor = RenderPass();
    //FragColor = vec4 (1.0, 0.0, 0.0, 0.0);
}
